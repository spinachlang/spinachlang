?start: (_NL* statement _NL*)+

%import common.WS_INLINE
%import common.NEWLINE -> _NL
%ignore WS_INLINE
%ignore /#[^\n]*/

statement: declaration
         | action

// Declarations
declaration: qubit_declaration
           | list_declaration
           | instruction_declaration

qubit_declaration: name ":" number
list_declaration: name ":" list
instruction_declaration: name ":" gate_pip

// Gate pipelines like A | B | my_instruction
gate_pip: gate ("|" (gate | name))*

// Allow: GATE, GATE(), GATE(arg, ...) 
gate: upper_name ["(" [args] ")"] -> gate_call
args: (name | number) ("," (name | number))*

// Actions: q1 -> gate or q1 -> my_instruction
action: name "->" [number] (name | gate_pip)

// List of names
list: "[" name ("," name)* "]"

// Identifiers
name: /[a-z][a-zA-Z0-9_]*/
upper_name: /[A-Z]+/
number: /\d+/

